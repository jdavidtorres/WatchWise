plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'co.com.jdti'
version = '0.0.1-SNAPSHOT'
description = 'Backend service for WatchWise.App'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // API documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

    // Database migration
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    runtimeOnly 'org.postgresql:postgresql'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // Mapping
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    def excludes = [
            'co/com/jdti/domain/**',
            'co/com/jdti/WatchWiseApiApplication*',
            'co/com/jdti/auth/JwtAuthenticationFilter*',
            'co/com/jdti/auth/SecurityConfig*',
            'co/com/jdti/auth/JwtProperties*',
            'co/com/jdti/config/**'
    ]
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: excludes)
    }))
}

jacocoTestCoverageVerification {
    def excludes = [
            'co/com/jdti/domain/**',
            'co/com/jdti/WatchWiseApiApplication*',
            'co/com/jdti/auth/JwtAuthenticationFilter*',
            'co/com/jdti/auth/SecurityConfig*',
            'co/com/jdti/auth/JwtProperties*',
            'co/com/jdti/config/**'
    ]
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: excludes)
    }))
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
        }
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
